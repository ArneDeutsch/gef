/*******************************************************************************
 * Copyright (c) 2008, 2015 Michael Clay and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *    Michael Clay    - initial API and implementation (#277380)
 *    Fabian Steeg    - updates for current Xtext and minor tweaks
 *    Alexander NyÃŸen - fixed grammar generation problems w.r.t. terminals (#429512)
 *                    - fixed problems resulting from non-optional subgraph names 
 *                      and shadowing of IDs by CommentPt literals (#430132)
 *                    - refactored grammar to be more consistent (#451097)
 * 
 *******************************************************************************/
grammar org.eclipse.gef4.internal.dot.parser.Dot hidden(WS, ML_COMMENT, SL_COMMENT)

generate dot "http://www.eclipse.org/gef4/internal/dot/parser/Dot"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

GraphvizModel:
	(graphs+=DotGraph)*;

DotGraph:
	(strict?="strict")? type=GraphType (name=ID)? "{"
		(stmts+=Stmt)*
	"}";

Stmt:
	(Attribute | EdgeStmtNode | EdgeStmtSubgraph | NodeStmt | AttrStmt | Subgraph) (";")?;

EdgeStmtNode:
	node=NodeId (edgeRHS+=EdgeRhs)+ (attrLists+=AttrList)*;

EdgeStmtSubgraph:
	subgraph=Subgraph (edgeRHS+=EdgeRhs)+ (attrLists+=AttrList)*;

NodeStmt:
	node=NodeId (attrLists+=AttrList)*;

AttrStmt:
	type=AttributeType (attrLists+=AttrList)+;

AttrList:
	"[" (attributes+=Attribute ("," | ";")? )+ "]";

Attribute:
	name=ID "=" value=ID;

Subgraph:
	{Subgraph} ("subgraph" name=ID?)? "{" (stmts+=Stmt)* "}";

Port:
	":" name=ID (":" compass_pt=CompassPt)? | ":" compass_pt=CompassPt;

EdgeRhs:
	(EdgeRhsNode | EdgeRhsSubgraph);

EdgeRhsNode:
	op=EdgeOp node=NodeId;

EdgeRhsSubgraph:
	op=EdgeOp subgraph=Subgraph;

NodeId:
	name=ID (port=Port)?;

// compass point values are no keywords (and thus allowed as part of an ID), 
// whereas node, edge, graph, digraph, subgraph, strict are keywords (and thus not allowed as part of an ID)
ID:
	STRING | QUOTED_STRING | NUMERAL | "n" | "ne" | "e" | "se" | "s" | "sw" | "w" | "nw" | "c" | "_";

enum EdgeOp:
	directed="->" | undirected="--";

enum GraphType:
	graph="graph" | digraph="digraph";

enum AttributeType:
	graph="graph" | node="node" | edge="edge";

enum CompassPt:
	north="n" | northeast="ne" | east="e" | southeast="se" | south="s" | southwest="sw" | west="w" | northwest="nw" |
	center="c" | wildcard="_";

terminal STRING:
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;
	
terminal NUMERAL: 
	('-')? ('.' ('0'..'9')+) | ('0'..'9')+ ('.' ('0'..'9')*)?;
	
terminal QUOTED_STRING:
	('"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | "'" | '\\') | !('\\' | '"'))* '"');

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	('//' | '#') !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal ANY_OTHER:
	.;
